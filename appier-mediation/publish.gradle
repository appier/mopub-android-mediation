apply plugin: 'maven-publish'

version = android.defaultConfig.versionName

ext {
    groupId = 'com.appier'
    artifactId = 'ads-mopub-mediation'
    url = 'https://www.appier.com/'
    scmUrl = 'https://bitbucket.org/plaxieappier/pmp-mopub-android-mediation/'
    licenseName = 'Apache-2.0'
    licenseUrl = 'https://www.appier.com'
    developerEmail = 'mobile@appier.com'
}
description = 'Appier Ads SDK: MoPub Mediation'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    // It's also a good practice for a closed-source SDK to expose some API interfaces
    // with source code, here we assume these has suffix of '.api' in there package name.
    include("**/api/*.java")
    include("**/api/*.kt")
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    include("**/api/*.java")
    include("**/api/*.kt")
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocJar(type: Jar) {
    from javadoc
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.productionRelease
                artifact sourcesJar {
                    classifier "sources" // this appends '-sources' to the filename of jar
                }
                artifact javadocJar {
                    classifier "javadoc" // this appends '-javadoc' to the filename of jar
                }
                groupId = project.groupId
                artifactId = project.artifactId
                version = this.version

                pom {
                    name = "${project.groupId}:${project.artifactId}"
                    description = project.description
                    url = project.url
                    scm {
                        url = project.scmUrl
                    }
                    licenses {
                        license {
                            name = project.licenseName
                            url = project.licenseUrl
                        }
                    }
                    developers {
                        developer {
                            email = project.developerEmail
                        }
                    }
                }
            }
        }
    }
}

apply from: "publish-maven-central.gradle"
apply from: "publish-bintray.gradle"