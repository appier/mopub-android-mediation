/**
 * Follow the example from bintray:
 * https://github.com/bintray/bintray-examples/tree/master/gradle-bintray-plugin-examples/android-gradle-3.0.0-maven-example
 */

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version android.defaultConfig.versionName
group 'com.appier.android'

ext {
    bintrayPkgRepo = 'maven' // existing repository in bintray to add the artifacts to
    publishedGroupId = 'com.appier.android'
    artifact = 'mopub-mediation' // bintray package name
    bintrayUserOrg = 'appier' // an optional organization name when the repo belongs to one of the user's orgs
    description = ''
    vcsUrl = 'https://bitbucket.org/plaxieappier/pmp-mopub-android-mediation.git'
}

task sourceJar(type: Jar) {
    archiveClassifier = "sources"
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact sourceJar
            artifact("$buildDir/outputs/aar/${project.getName()}-production-release.aar")
            groupId publishedGroupId
            artifactId artifact
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ?: System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ?: System.getenv('BINTRAY_API_KEY')
    publications = ['Production']
    configurations = ['archives']
    override = true

    pkg {
        repo = bintrayPkgRepo
        name = artifact
        desc = description
        publish = true
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        userOrg = bintrayUserOrg
        vcsUrl = vcsUrl

        version {
            name = this.version
            desc = description
            released = new Date()
            vcsTag = this.version
        }
    }
}
